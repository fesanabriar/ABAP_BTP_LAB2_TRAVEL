managed;   // El manejo lo hace el Framework, no el desarrollador
//strict ( 2 ); //Valida que el codigo este escrito de manera muy estricta (1,2)
//with draft; //
//--------------------------------------------------------------------------------------------------------------------
//BEHAVIOR TRAVEL
//--------------------------------------------------------------------------------------------------------------------
define behavior for ZCD_TRAVEL_7143 alias Travel
implementation in class zcl_travel_7143 unique //Clase manejadora para este Behavior
persistent table ztb_travel_7143 //Guardado por el Framework
//////draft table ztbd_travel_7143 // Crea Temporal en una copia de la tabla pero DRAF
lock master   //Bloqueo de instancia
authorization master ( instance )//Control de autorizaciones
etag master LastChangedAt //Asegura que no se den actualizace en el protocolo OData simultaneas
{
  // Administrative fields Read Only
  field ( readonly ) TravelID;
  field ( readonly ) LastChangedAt, LastChangedBy, CreatedAt, CreatedBy;
  // Mandatory fields that are required to create a Travel
  field ( mandatory ) AgencyId, OverallStatus, BookingFee, CurrencyCode;
  // Dinamic field control
  field ( features : instance ) BookingFee; //TravelId;

  // Mapping - mapeo a la tabla de base de datos
  mapping for ztb_travel_7143 //corresponding;
    {
      TravelID      = travel_id;
      AgencyID      = agency_id;
      CustomerID    = customer_id;
      BeginDate     = begin_date;
      EndDate       = end_date;
      BookingFee    = booking_fee;
      TotalPrice    = total_price;
      CurrencyCode  = currency_code;
      Description   = description;
      OverallStatus = overall_status;
      CreatedBy     = created_by;
      CreatedAt     = created_at;
      LastChangedBy = last_changed_by;
      LastChangedAt = last_changed_at;
      //      LastChangedAt      = last_changed_at;
    }
  create;
  update;
  delete;

  // Actions
  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;

  action createTravelByTemplate result [1] $self;
  internal action reCalcTotPrice; //Se ejecutaria cada cambio de datos


  // Validations
  validation validateCustomer on save { field CustomerId; }
  validation validateDates on save { field BeginDate, EndDate; }
  validation validateStatus on save { field OverallStatus; }

  association _Booking { create; }

  //////  determination setTravelNumber on save { create; } //Se ejcuta con el save
  //////  determination setStatusToOpen on modify { create; } // Se ejcuta con cada modificacion
  //////  determination CalcTotalPrice  on modify { create; field BookingFee, CurrencyCode; } // Se ejcuta con cada modificacion especifica



  //////  //  Determinacion de accion
  //////  determine action validateCustomerId { validation validateCustomer; }
  //////  determine action validateDate       { validation validateDates; }
  //////  determine action validateStatuss    { validation validateStatus; }

}
//--------------------------------------------------------------------------------------------------------------------
//BEHAVIOR BOOKING
//--------------------------------------------------------------------------------------------------------------------
define behavior for ZCD_BOOKING_7143 alias Booking
implementation in class zcl_booking_7143 unique //Clase manejadora para este Behavior
persistent table ztb_booking_7143 //Guardado por el Framework
lock dependent by _Travel
authorization dependent by _Travel //Control de autorizaciones
etag master LastChangedAt //Asegura que no se den actualizace en el protocolo OData simultaneas
{
  //////  // Static field control
  field ( mandatory ) CarrierId, ConnectionId, FlightDate, BookingStatus;
  field ( readonly ) TravelId, BookingId;

  // Mapping - mapeo a la tabla de base de datos
  mapping for ztb_booking_7143 corresponding;

  update;
  delete;

  // Validations
  validation validateStatus on save { field BookingStatus; }

  // Determination - cálculo del precio total del vuelo
  determination calculateTotalFlightPrice on modify { field FlightPrice, CurrencyCode; }

  // Associations
  association _Supplement { create; }
  association _Travel {}
}

//--------------------------------------------------------------------------------------------------------------------
//BEHAVIOR SUPLEMENT
//--------------------------------------------------------------------------------------------------------------------
define behavior for ZCD_BOOKPSUPL_7143 alias Supplement
implementation in class zcl_supplement_7143 unique //Clase manejadora para este Behavior
with unmanaged save //guardado no administrado por el framework
//persistent table ztb_booksup_7143
lock dependent by _Travel
authorization dependent by _Travel //Control de autorizaciones
etag master LastChangedAt //Asegura que no se den actualizace en el protocolo OData simultaneas
{
  // Static field control
  field ( mandatory ) SupplementId, Price;
  field ( readonly ) TravelId, BookingId;

  // Mapping - mapeo del CDS a base de datos
  mapping for ztb_log_7143 corresponding; //No entendi porque esta tabal y no la ZTB_BOOKPSUPL_7143

  update;
  delete;

  // Determination - cálculo del precio total del vuelo
  determination calculateTotalSupplinPrice on modify { field Price, CurrencyCode; }

  // Associations
  association _Travel { }
  association _Booking { }

}